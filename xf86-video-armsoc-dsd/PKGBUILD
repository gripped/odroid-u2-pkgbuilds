# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>
# Modifier:   gripped forum.odroid.com

# Official upstream is at 
# http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git;a=summary

buildarch=4

pkgname=xf86-video-armsoc-dsd
pkgdesc="armsoc framebuffer driver for X from chromium os git with exynos drm mods"
pkgver=210.3516aa3
pkgrel=1
epoch=1
arch=('armv7h')
url="https://github.com/dsd/xf86-video-armsoc.git"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=('xf86-video-armsoc' 'xf86-video-armsoc-git')
replaces=('xf86-video-armsoc' 'xf86-video-armsoc-git')
provides=('xf86-video-armsoc' 'xf86-video-armsoc-git')
source=("$pkgname::git+https://github.com/dsd/xf86-video-armsoc.git#branch=r4p0")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  make -C "$srcdir/$pkgname" DESTDIR="${pkgdir}" install
}
