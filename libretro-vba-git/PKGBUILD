# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-vba-git
pkgver=424.01f80d6
pkgrel=1
pkgdesc="libretro implementation of VBA Next. (Game Boy Advance)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/vba-next"
license=('GPL')
makedepends=('git')

_gitname=vba-next
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/vba_next_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  sed -i 's:CFLAGS += -O3:CFLAGS += -O2:g' Makefile.libretro
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/vba_next_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-vba.so"
  install -Dm644 "vba_next_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-vba.info"
}
