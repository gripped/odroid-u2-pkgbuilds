# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-nestopia-git
pkgver=383.d18cf12
pkgrel=1
pkgdesc="libretro implementation of NEStopia. (Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/nestopia"
license=('GPL')
makedepends=('git')

_gitname=nestopia
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/nestopia_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/libretro"
  sed -i 's:CFLAGS += -O3:CFLAGS += -O2:g' Makefile
  sed -i 's:CXXFLAGS += -O3:CXXFLAGS += -O2:g' Makefile
  make
}

package() {
  install -Dm644 "${_gitname}/libretro/nestopia_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-nestopia.so"
  install -Dm644 "nestopia_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-nestopia.info"
}
